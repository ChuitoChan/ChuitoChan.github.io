---
layout: post
title: 2048 in Clojure
feature-img: "img/sample_feature_img_2.png"
---

上一篇博文好像水过头了，这次就来点认真的吧——用`Clojure`写个`2048`小游戏。
由于我是用`Clojure`写的，所以实现起来会跟其他语言有点不同哦。<br>
一开始先用`vector`定义一个空面板（用0表示空格子）：
{% highlight clojure linenos %}
(def blank-board
  (vec (repeat 4 (vec (repeat 4 0)))))
 
blank-board
;=> [[0 0 0 0] [0 0 0 0] [0 0 0 0] [0 0 0 0]]
{% endhighlight clojure %}

然后再用`sorted-map`定义一下划动的方向，不同方向对应不同的值：
{% highlight clojure linenos %}
(def directions (sorted-map :left 0 :down 1 :right 2 :up 3))
directions
;=> {:down 1, :left 0, :right 2, :up 3}
{% endhighlight clojure %}

#### 填格子：
1. 找出所有空着的格子
{% highlight clojure linenos %}
(defn blanks [board]
  (for [x (range 4)
        y (range 4)
        :when (zero? (get-in board [x y]))]
    [x y]))
{% endhighlight clojure %}

2. 随机给一个空格子填入2或4（概率分别为P(2)=0.9，P(4)=0.1）:
{% highlight clojure linenos %}
(defn add-tile [board]
  (let [elem (rand-nth [2 2 2 2 4 2 2 2 2 2])
        pos  (rand-nth (blanks board))]
    (assoc-in board pos elem)))
{% endhighlight clojure %}

将面板打印出来，并且返回面板：
{% highlight clojure linenos %}
(defn print-board [board]
  (do
    (doseq [x (range 4)]
      (doseq [y (range 4)]
        (printf "%8d" (get-in board [x y])))
      (println))
    (println "--------------------------------")
    board))
(print-board (add-tile blank-board))
;=> 
{% endhighlight clojure %}


#### 划动
1. 先考虑一行里格子的碰撞合并，思路是先去掉空的格子，再将相邻值相等的格子分在一块，再将每个格子块分割成两个一组，最后将每组的数（两个或一个）分别加起来：
{% highlight clojure linenos %}
(defn add-pairs [row]
  (->> row
       (remove zero?)
       (partition-by identity)
       (mapcat #(partition-all 2 %))
       (map #(apply + %))))
{% endhighlight clojure %}

2. 补充空格子：
{% highlight clojure linenos %}
(defn padding [row]
  (take 4 (concat row (repeat 0))))
  
(padding (add-pairs [4 4 0 8]))
;=> (8 8 0 0)
{% endhighlight clojure %}


3. 上面两个操作针对的是左划，那么其他方向的划动怎么实现呢？这个有点巧妙：譬如我们要向下划动，我们可以先将面板顺时针旋转90度，这样方向向下的列就变成方向向左的行了，然后就可以用上面的`add-pairs`和`padding`对每一行进行处理，处理完之后再逆时针旋转回来就是了（实际是顺时针转多270度）。所以前面`directions`里面划动方向对应的值其实就是它要顺时针旋转原面板90度的次数。这样剩下的问题就是怎么实现旋转一个面板了，这也很简单：先将原面板的行颠倒一下顺序，然后对面板转置，就能得到旋转后的面板。
{% highlight clojure linenos %}
(defn rotate [board]
  (apply mapv vector (reverse board)))

(defn rotate-board [n board]
  (nth (iterate rotate board) n))
{% endhighlight clojure %}


4. 最后定义一个`move`实现整个划动操作：
{% highlight clojure linenos %}
(defn move [board dir]
  (->> board
       (rotate-board (directions dir))
       (map add-pairs)
       (map padding)
       (rotate-board (- 4 (directions dir)))))
{% endhighlight clojure %}

#### 游戏控制
1. 初始化：
{% highlight clojure linenos %}
(defn init-game []
  (-> blank-board
      add-tile
      add-tile))
{% endhighlight clojure %}


2. 判断是否game over（也就是哪个方向都动不了）：
{% highlight clojure linenos %}
(defn noop? [board dir]
  (= board (move board dir)))
  
(defn gameover? [board]
  (every? (partial noop? board) (keys directions)))
{% endhighlight clojure %}

3. 一轮操作的过程：如果划动之后的面板和原来一样，则不填入新的格子：
{% highlight clojure linenos %}
(defn play [board dir]
  (-> board
      (move dir)
      (as-> new-board
            (if (= board new-board)
              new-board
              (add-tile new-board)))))
{% endhighlight clojure %}

4. 整个游戏的运行：定义一个`auto-play`函数接收2个参数：原面板`board`和函数`player`，先用`player`根据原面板返回一个划动方向`dir`（并且将它显示出来），进行一轮游戏，产生新面板`res`，将新面板打印出来，如果游戏没有结束，就用新面板尾递归回去
{% highlight clojure linenos %}
(defn auto-play [board player]
  (let [dir (player board)
        _   (prn dir)
        res (play board dir)]
    (print-board res)
    (if (gameover? res)
      (println "Game over!")
      (recur res player))))
{% endhighlight clojure %}


这样，基本上一个2048的游戏核心就出来啦！！！<br>
<br>
<br>



以为这样就结束了吗？呵呵！我告诉你：ed后有正片！有正片！
<br>
<br>
<br>




-------------------我是不要脸的分割线------------------------
<br>
<br>
光写个游戏出来太简单了，感觉还不够好玩，身为一名技术宅，不仅要能写游戏，还得会玩游戏，也就是俺要开挂~~ 虽然我之前玩2048经常犯二，但是我可以写个没那么逗逼的小AI帮我去玩啊！于是我就决定写个2048小AI（给自己挖个坑）。
然后就找到了这篇[2048-AI算法分析](http://blog.codinglabs.org/articles/2048-ai-analysis.html)，学习了如何用[Minimax](https://en.wikipedia.org/wiki/Minimax)（也就是极小化极大算法）和Alpha-beta剪枝实现2048-AI。还发现了`stackoverflow`上两位大神关于[What is the optimal algorithm for the game 2048?](http://stackoverflow.com/questions/22342854/what-is-the-optimal-algorithm-for-the-game-2048)的回答。然后我就去找两位大神的源码来研究。[ovolve](https://github.com/ov3y/2048-AI)大神的AI是基于[Minimax](https://en.wikipedia.org/wiki/Minimax)和[Alpha-beta剪枝](https://en.wikipedia.org/wiki/Alpha%E2%80%93beta_pruning)算法用`javascript`实现的，而[nneonneo](https://github.com/nneonneo/2048-ai)大神的AI是基于[Expectimax](https://en.wikipedia.org/wiki/Expectiminimax_tree)算法用`C++`实现的。但是这种两种语言我都没学过啊，只能根据代码去大概揣测真正的意图。但是大神就是大神，各种高级的编程技巧，岂是我这渣渣所能领悟的呢？重要的是我还得转化成`Clojure`来实现呀，各种蛋疼~~~~~~~~
大神们关于启发函数考虑了不少评判因素，比如空格子的个数，单调性，平整度，最大值等等。但是我只是个初学编程的战五渣呀，不要求像`nneonneo`大神那样100%达到8192，只需较大机率获得2048就心满意足了。所以我就没用大神的启发函数。


#### 建模
引用[2048-AI算法分析](http://blog.codinglabs.org/articles/2048-ai-analysis.html)的：

        我方：游戏玩家。每次可以选择上、下、左、右四个行棋策略中的一种（某些格局会少于四种，因为有些方向不可走）行棋后方块按照既定逻辑移动及合并，格局转换完成。
        对方：计算机。在当前任意空格子里放置一个方块，方块的数值可以是2或4。放置新方块后，格局转换完成。
        胜利条件：出现某个方块的数值为“2048”。
        失败条件：格子全满，且无法向四个方向中任何一个方向移动（均不能触发合并）。

#### 格局评价
我并没有采用大神那么多指标，而是采用我之前玩2048的思路（虽然我之前玩的时候时不时会傻逼一下，然后就跪了）。大家玩过都知道最方便合并的格局就是将最大那个数保持在角落，并且沿S路线单调递减，也就下面两个图那样（以左上角为例）。
<img src="/img/2048_row.png" alt="横向S路线" title="横向S路线" width="250" /><img src="/img/2048_col.png" alt="纵向S路线" title="纵向S路线" width="250" />
<br>
为了让格子随着S路线单调递减，将评价函数定义为S路线上格子的值与一个等比数列（公比0 < p < 1）对应的项的乘积之和。

在计算机回合，由于计算机放置方块后，评价函数的值肯定是增加的，因此这个时候的评价会有所不同。想了想玩游戏时候的情况，最蛋疼应该是在那条单调递减路线中间被电脑强插了一个“2”，破坏了整体的单调性。所以我将计算机回合的操作统一定为在评价分数最高的那条路线上找到最靠前的空格子并且插入一个“2”.这样做的结果就是省去了不少电脑回合对评价函数的计算。
在玩家回合，我并没有像`Minimax`那样，搜索树的节点取其子节点的最大值。因为实际运行过程中，我发现会有过度悲观的问题。当我们处于较好的格局时，在几次假设的最坏情况后，那些一开始有利于维持良好格局的方向的叶子，可能会不如那些一开始就破坏良好格局的方向的叶子，因此如果只考虑搜索树的叶子的最大值的话就会得出最优方案是那些一开始会破坏良好格局的方向。但是不同于真正的博弈，电脑对空格子的填入并不是理性的，而是随机，连续发生几次最坏情况的机率是很低的。为了避免那发生机率很低的最最最坏情况而打破原来的良好格局，是很不划算的。所以我在决定那个方向是最优方向时，考虑的是整个搜索的态势，叶子的启发得分就是其评价分数，而节点的启发得分则是其子节点的最大启发得分乘以一定比值h（h>1，因为最坏情况下得到的高分更有价值）再加上该节点本身的评价分数。因此能搜索到的层数越多，得分越高。

#### 实现
1. 先定义等比数列，以0.25为公比：
{% highlight clojure linenos %}
(def coefs (take 16 (iterate #(* 0.25 %) 1)))
{% endhighlight clojure %}


2. 对面板上每个格子将其值和坐标保存到一个map里面
{% highlight clojure linenos %}
(defn val-pos [board]
  (for [row (range 4)]
    (for [col (range 4)]
      {:value (get-in board [row col]) :pos [row col]})))
{% endhighlight clojure %}


3. 以左上角为例，定义`row-path`和 `col-path`分别求横向和纵向的S路线。其他角的S路线可利用之前的定义`rotate`先旋转面板，再用`row-path`和`col-path`求得
{% highlight clojure linenos %}
(defn row-path [board]
  (concat (first board) (reverse (second board))
          (last (butlast board)) (reverse (last board))))

(defn col-path [board]
  (let [rev-board (apply mapv vector board)]
    (row-path rev-board)))
{% endhighlight clojure %}

4. 求最高评价分数与待填空格子（也就是影响单调性最大的格子）的坐标
{% highlight clojure linenos %}
(defn calculate [path]
  (reduce + (map * (map :value path) coefs)))

(defn critical-blank [path]
  (:pos (first (filter (comp zero? :value) path))))
  
(defn evaluate [board]
  (let [v-p-board (val-pos board)
        boards    (take 4 (iterate rotate v-p-board))
        paths     (concat (map row-path boards) (map col-path boards))
        best-path (last (sort-by calculate paths))]
    {:score (calculate best-path) :pos (critical-blank best-path)}))
{% endhighlight clojure %}
* `calculate`计算一条路线的评价分数
* `critical-blank`找出路径上最靠前的空格子的坐标
* `evaluate`找出面板中评价分数最高的那一条路线，并返回该路线关于评价分数和待填入空格子坐标的map

7. 接下来是最长，最复杂的一个函数——`next-move`，用来决定下一步的划动方向。它接收3个参数：原面板`board`、当前搜索深度`depth`和最大搜索深度`max-depth`，返回一个关于最优划动方向，该划动方向的评价分数和划动该方向后对单调性影响最大的空格子坐标的map。
{% highlight clojure linenos %}
(defn next-move [board depth max-depth]
  (let [dirs (remove #(noop? board %) (keys directions))]
    (if (empty? dirs)
      {:dir :left :score 0 :pos [0 0]}
      (if (zero? depth)
        (->> dirs
             (map #(move board %))
             (map evaluate)
             (map #(assoc %2 :dir %1) dirs)
             (sort-by :score)
             last)
        (let [boards     (map #(move board %) dirs)
              scores-pos (map evaluate boards)
              scores     (map :score scores-pos)
              pos        (map :pos scores-pos)
              new-boards (map #(assoc-in %1 %2 2) boards pos)]
          (->> new-boards
               (map #(next-move % (dec depth) max-depth))
               (map #(+ %1 (/ (:score %2) 
                              (Math/pow 0.9 (+ (- max-depth depth) 1))))
                    scores)
               (map #(hash-map :pos %1 :score %2) pos)
               (map #(assoc %2 :dir %1) dirs)
               (sort-by :score)
               last))))))
{% endhighlight clojure %}

* 先获取有效的划动方向
* 如果没有有效的划动方向，则默认返回评价分数为0,划动方向向左，空格子坐标为[0 0]（划动方向和格子坐标并不重要）
* 如果搜索深度为0，则对原面板分别进行每个有效的划动操作，计算对应的评价分数和影响单调性最大的空格子的坐标，转换成对应的map,最后排序，返回分数最大的那一个map
* 如果搜索深度不为0，则先对原面板分别进行每个有效的划动操作得到`boards`，再计算对应的评价分数和影响单调性最大的空格子的坐标得到`scores-pos`，分别将评价分数和空格子坐标保存为`scores`和`pos`。对`boards`分别在对应的`pos`位置上填入“2”得到`new-boards`。对`new-boards`里的每个面板调用深度减1的`next-move`，返回对应的包含最优划动方向和评价分数的map。再提取出评价分数，除以0.9（因为我还没想到这个比值h应该怎么取才合适，所以随便扔个数进去试试先），与`scores`里未填入“2”时的评价分数相加得到新的评价分数。最后就是排序，返回分数最大的那一个map

8. 最后定义一个`ai-player`返回特定搜索深度（如3）的最优划动方向。以及一个`-main`函数运行游戏。
{% highlight clojure linenos %}
(defn ai-player [board]
  (:dir (next-move board 3 3)))

(defn -main []
  (auto-play (init-game) ai-player))
{% endhighlight clojure %}

运行一下`-main`，然后就“啾，啾。。。”，结果在168秒内跑出了这个：

        4       2     512       4
      256      16    1024       2
      128      64      32       8
        4       8       4    2048
<br>
感动的泪流满面啊~~~~~~~~~

为了验证下效果，跑了100遍，结果如下，93%的成功率还算不错(๑•̀ㅂ•́)و✧要是能跑出一个16384就好了
<br>
![实验结果](/img/2048_chart.png)

## Code
The code can be found on GitHub at the following link [https://github.com/ChuitoChan/clojure-2048ai](https://github.com/ChuitoChan/clojure-2048ai)
